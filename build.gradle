/**
 *  Root-level JmeConvert build script.
 */

plugins {
    id 'jmec.published-library'
    id 'application'
    id "com.gorylenko.gradle-git-properties" version "2.4.0"
}

version='1.3.1-SNAPSHOT'

mainClassName='com.simsilica.jmec.Convert'


applicationDefaultJvmArgs = ["-Xmx4g", "-Xms512m", "-XX:MaxDirectMemorySize=1024m"]

ext.jmeVersion = "3.3.2-stable"

// Set this module's maven pom description
publishing.publications.library(MavenPublication).pom {
    description = 'A command line utility and embeddable Java library for converting models to jMonkeyEngine format.'
}

dependencies {
    api "org.jmonkeyengine:jme3-core:$jmeVersion"

    // Make sure we have all of the plugins, loaders, etc.
    implementation "org.jmonkeyengine:jme3-desktop:$jmeVersion"
    implementation "org.jmonkeyengine:jme3-plugins:$jmeVersion"
    //runtimeOnly "org.jmonkeyengine:jme3-blender:$jmeVersion"
    // Blender is a separate project now and not well supported.

    api 'com.google.guava:guava:19.0'

    // For runtime scripting support
    runtimeOnly 'org.codehaus.groovy:groovy-all:2.4.11'

    implementation 'org.slf4j:slf4j-api:1.7.13'
    implementation 'org.slf4j:jul-to-slf4j:1.7.13'
    runtimeOnly 'org.apache.logging.log4j:log4j-api:2.5'
    runtimeOnly 'org.apache.logging.log4j:log4j-core:2.5'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'

    // Trying to get log coloring working properly on windows
    runtimeOnly 'org.fusesource.jansi:jansi:1.11'

}

compileJava.doLast {
    def buildDate = new Date().format('yyyyMMdd')
    println "Writing jmec.build.date:" + buildDate
    new File(destinationDir, 'jmec.build.date').text = "build.date=$buildDate"
}

